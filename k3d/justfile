name := "pangolin"
path := "../"
k3d-config := "k3d.yaml"
kubeconfig := "~/.kube/config"

# List all commands
list:
    just --list --unsorted

########################################################
# K3d

# Create k3d cluster
create-cluster:
    k3d cluster create {{ name }} --config {{ k3d-config }}

# Delete k3d cluster
delete-cluster:
    k3d cluster delete {{ name }}

########################################################
# Helm

_helm *args:
    helm {{ args }} \
        --namespace {{ name }} \
        --kubeconfig {{ kubeconfig }} \
        --kube-context k3d-{{ name }}

# Install chart
install:
    @echo "Updating dependencies..."
    helm dependency update {{ path }}
    @echo
    @echo "Installing chart..."
    just _helm upgrade \
        --install {{ name }} {{ path }} \
        --create-namespace \
        --values {{ path }}values.yaml \
        --values {{ path }}values-dev.yaml

# Uninstall chart
uninstall:
    just _helm uninstall {{ name }}
